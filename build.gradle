buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE"
  }
}

ext {
	jaxrs = "2.1.1"
	cxf = "3.2.6"
	junit = "5.3.1"
	mockito = "2.22.0"
	spring = "2.0.5.RELEASE"
}

allprojects{
	apply plugin: "java"
	apply plugin: "eclipse"

	repositories {
		mavenCentral()
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	group = 'com.github.arucard21.simplyrestful'
	version = '0.9.0'
}

subprojects{
	apply plugin: 'maven'
	apply plugin: 'signing'

	test {
		useJUnitPlatform()
	}
	
	sourceSets {
		integrationTest {
			java {
				compileClasspath += main.output + test.output
				runtimeClasspath += main.output + test.output
			}
		}
	}
	
	configurations {
		integrationTestCompile.extendsFrom testCompile
		integrationTestRuntime.extendsFrom testRuntime
	}
	
	task integrationTest(type:Test){
		useJUnitPlatform()
		testClassesDir = project.sourceSets.integrationTest.output.classesDir
		classpath = project.sourceSets.integrationTest.runtimeClasspath
	}
	
	eclipse {
		classpath {
			plusConfigurations += [configurations.integrationTestCompile]
		}
	}
}

project(":SimplyRESTful-resources"){
	project.description = "The core API resources used in SimplyRESTful APIs"
	dependencies {
		compile ("io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.4")
		compile ("io.openapitools.hal:swagger-hal:1.0.4")
	}
}

project(":SimplyRESTful"){
	project.description = "A framework for creating a RESTful API"
	dependencies {
		compile project(":SimplyRESTful-resources")
		compile ("javax.inject:javax.inject:1")
		compile ("javax.ws.rs:javax.ws.rs-api:${jaxrs}")
		compile ("javax.validation:validation-api:2.0.1.Final")
		compile ("io.swagger:swagger-annotations:1.5.21")

		testCompile ("org.mockito:mockito-core:${mockito}")
		testCompile ("org.mockito:mockito-junit-jupiter:${mockito}")
		testCompile ("org.junit.jupiter:junit-jupiter-api:${junit}")
		testCompile ("org.junit.jupiter:junit-jupiter-engine:${junit}")
		testCompile ("org.junit.platform:junit-platform-launcher:1.3.1")
		testCompile ("org.glassfish.jersey.containers:jersey-container-servlet:2.27")
		
		integrationTestCompile ("org.apache.cxf:cxf-rt-transports-local:${cxf}")
		integrationTestCompile ("org.apache.cxf:cxf-rt-frontend-jaxrs:${cxf}")
		integrationTestCompile ("org.apache.cxf:cxf-rt-rs-client:${cxf}")
	}
}

project(":client"){
	project.description = "A generic client that can be used with any SimplyRESTful API"
	dependencies {
		compile project(":SimplyRESTful-resources")
		compile ("org.glassfish.jersey.core:jersey-client:2.27")
		compile ("io.swagger:swagger-models:1.5.21")
	}
}

project(":deploy-springboot-cxf"){
	project.description = "Easily deploy your SimplyRESTful API using Apache CXF with Spring Boot"
	dependencies {
		compile project(":SimplyRESTful")
		compile ("org.webjars:swagger-ui:2.2.6")
		compile ("org.apache.cxf:cxf-spring-boot-starter-jaxrs:${cxf}")
		compile ("org.apache.cxf:cxf-rt-rs-service-description-swagger:${cxf}")
		compile ("org.apache.cxf:cxf-rt-rs-extension-search:${cxf}")
	}
}

project(":deploy-springboot-jersey"){
	project.description = "Easily deploy your SimplyRESTful API using Jersey with Spring Boot"
	dependencies {
		compile project(':SimplyRESTful')
		compile ("org.springframework.boot:spring-boot:${spring}")
		compile ("org.webjars:swagger-ui:2.2.6")
		compile ("org.springframework.boot:spring-boot-starter-jersey:${spring}"){
			exclude group: "org.glassfish.jersey.media", module: "jersey-media-json-jackson"
		}
		compile ("io.swagger:swagger-jersey2-jaxrs:1.5.21")
	}
}

project(":deploy-jetty-cxf"){
	project.description = "Easily deploy your SimplyRESTful API using Apache CXF"
	dependencies {
		compile project(':SimplyRESTful')
		compile ("org.apache.cxf:cxf-rt-frontend-jaxrs:${cxf}")
		compile ("org.apache.cxf:cxf-rt-rs-service-description-swagger:${cxf}")
		compile ("org.apache.cxf:cxf-rt-rs-extension-search:${cxf}")
		compile ("org.webjars:swagger-ui:2.2.6")
		compile ("ch.qos.logback:logback-classic:1.2.1")
		compile ("org.hibernate:hibernate-validator:5.4.0.Final")
		compile ("org.glassfish:javax.el:3.0.1-b08")
	}
}

project(":persist-springdata"){
	project.description = "Easily persist your SimplyRESTful API using Spring Data"
	dependencies {
		compile project(':SimplyRESTful')
		compile ("org.springframework.boot:spring-boot-starter-data-jpa:${spring}")
	}
}

project(":examples:datastore"){
	uploadArchives.enabled = false;
}

project(":examples:springboot-cxf"){
	apply plugin: "org.springframework.boot"
	project.description = "Example project for a SimplyRESTful API using Apache CXF with Spring Boot"
	dependencies {
		compile project(':SimplyRESTful')
		compile project(':deploy-springboot-cxf')
		compile project(':examples:datastore')
	}
	uploadArchives.enabled = false;
}

project(":examples:jetty-cxf"){
	project.description = "Example project for a SimplyRESTful API using Apache CXF on Jetty"
	dependencies {
		compile project(':SimplyRESTful')
		compile project(':deploy-jetty-cxf')
		compile project(':examples:datastore')
		compile ("org.apache.cxf:cxf-rt-transports-http-jetty:${cxf}")
	}
	uploadArchives.enabled = false;
}

project(":examples:springboot-jersey-nomapping-springdata"){
	apply plugin: "org.springframework.boot"
	project.description = "Example project for a SimplyRESTful API using Jersey with Spring Boot and directly stored with Spring Data"
	dependencies {
		compile project(':deploy-springboot-jersey')
		compile project(':persist-springdata')
		compile ("com.h2database:h2:1.4.197")
	}
	uploadArchives.enabled = false;
}

// Provide tasks to publish projects to Maven Central
configure(subprojects - project(":examples").allprojects){
	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment ->
					signing.signPom(deployment)
				}

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name project.name
					packaging 'jar'

					description project.description
					url = 'https://github.com/arucard21/SimplyRESTful-Framework'

					scm {
						connection 'scm:git:git://github.com:arucard21/SimplyRESTful-Framework.git'
						developerConnection 'scm:git:ssh://github.com:arucard21/SimplyRESTful-Framework.git'
						url 'https://github.com/arucard21/SimplyRESTful-Framework'
					}

					licenses {
						license {
							name 'GNU Lesser General Public License'
							url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
						}
					}

					developers {
						developer {
							name 'Riaas Mokiem'
							email 'arucard21@gmail.com'
							organizationUrl 'https://github.com/arucard21'
						}
					}
				}
			}
		}
	}
}
