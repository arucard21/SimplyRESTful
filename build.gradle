allprojects{
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: 'maven'
	apply plugin: 'signing'

	repositories {
		mavenCentral()
	}

	sourceCompatibility = JavaVersion.VERSION_1_10
	targetCompatibility = JavaVersion.VERSION_1_10

	group = 'com.github.arucard21.simplyrestful'
	version = '0.7'

	javadoc {
		options.addBooleanOption('html5', true)
	}

	test {
		useJUnitPlatform()
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment ->
					signing.signPom(deployment)
				}

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name 'SimplyRESTful'
					packaging 'jar'

					description 'A framework for creating a RESTful API'
					url = 'https://github.com/arucard21/SimplyRESTful'

					scm {
						connection 'scm:git:git://github.com:arucard21/SimplyRESTful.git'
						developerConnection 'scm:git:ssh://github.com:arucard21/SimplyRESTful.git'
						url 'https://github.com/arucard21/SimplyRESTful/tree/master'
					}

					licenses {
						license {
							name 'GNU Lesser General Public License'
							url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
						}
					}

					developers {
						developer {
							name 'Riaas Mokiem'
							email 'arucard21@gmail.com'
							organizationUrl 'https://github.com/arucard21'
						}
					}
				}
			}
		}
	}
}

ext {
	jaxrs = "2.1.1"
	cxf = "3.2.6"
	junit = "5.3.1"
	mockito = "2.22.0"
}

compileJava {
	options.compilerArgs += ["--add-modules", "java.se.ee"]
}

compileTestJava {
	options.compilerArgs += ["--add-modules", "java.se.ee"]
}

test {
	jvmArgs += ["--add-modules", "java.se.ee"]
}

dependencies {
	compile ("javax.ws.rs:javax.ws.rs-api:"+jaxrs)
	compile ("javax.validation:validation-api:2.0.1.Final")
	compile ("javax.inject:javax.inject:1")
	compile ("io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.4")
	compile ("io.swagger:swagger-annotations:1.5.21")
	
	testCompile ("org.mockito:mockito-core:"+mockito)
	testCompile ("org.mockito:mockito-junit-jupiter:"+mockito)
	testCompile ("org.glassfish.jersey.containers:jersey-container-servlet:2.27")
	testCompile ("org.junit.jupiter:junit-jupiter-api:"+junit)
	testCompile ("org.junit.jupiter:junit-jupiter-engine:"+junit)
	testCompile ("org.junit.platform:junit-platform-launcher:1.3.1")
}

project(":SimplyRESTful-cxf-spring-boot"){
	compileJava {
		options.compilerArgs += ["--add-modules", "java.se.ee"]
	}
	
	compileTestJava {
		options.compilerArgs += ["--add-modules", "java.se.ee"]
	}
	
	test {
		jvmArgs += ["--add-modules", "java.se.ee"]
	}
	dependencies {
		compile ("org.apache.cxf:cxf-spring-boot-starter-jaxrs:"+cxf)
		compile ("org.apache.cxf:cxf-rt-rs-service-description-swagger:"+cxf)
		compile ("org.apache.cxf:cxf-rt-rs-extension-search:"+cxf)
		compile ("org.webjars:swagger-ui:2.2.6")
		compile ("dk.nykredit.jackson.dataformat:jackson-dataformat-hal:1.0.1")
	}
}

project(":SimplyRESTful-cxf"){
	compileJava {
		options.compilerArgs += ["--add-modules", "java.se.ee"]
	}
	
	compileTestJava {
		options.compilerArgs += ["--add-modules", "java.se.ee"]
	}
	
	test {
		jvmArgs += ["--add-modules", "java.se.ee"]
	}

	dependencies {
		compile ("org.apache.cxf:cxf-rt-transports-http-jetty:"+cxf)
		compile ("org.apache.cxf:cxf-rt-frontend-jaxrs:"+cxf)
		compile ("org.apache.cxf:cxf-rt-rs-service-description-swagger:"+cxf)
		compile ("org.apache.cxf:cxf-rt-rs-extension-search:"+cxf)
		compile ("org.webjars:swagger-ui:2.2.6")
		compile ("ch.qos.logback:logback-classic:1.2.1")
		compile ("org.hibernate:hibernate-validator:5.4.0.Final")
		compile ("org.glassfish:javax.el:3.0.1-b08")
		compile ("dk.nykredit.jackson.dataformat:jackson-dataformat-hal:1.0.1")
	}
}

project(":SimplyRESTful-example:datastore"){
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

project(":SimplyRESTful-example:spring-boot"){
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	dependencies {
		compile project(':SimplyRESTful-example:datastore')
		
		compile ("com.github.arucard21.simplyrestful:simplyrestful:0.6")
		compile ("com.github.arucard21.simplyrestful:simplyrestful-spring-boot:0.2")
		compile ("javax.inject:javax.inject:1")
	}
}

project(":SimplyRESTful-example:jetty"){
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	dependencies {
		compile project(':SimplyRESTful-example:datastore')
		
		compile ("com.github.arucard21.simplyrestful:simplyrestful:0.6")
		compile ("com.github.arucard21.simplyrestful:simplyrestful-jetty:0.5")
	}
}
